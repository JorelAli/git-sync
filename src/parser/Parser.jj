options {
	STATIC = false;
}

PARSER_BEGIN(Parser)

package parser;

import java.io.ByteArrayInputStream;
import java.io.InputStream;
import java.nio.charset.Charset;
import java.util.ArrayList;
import java.util.List;
import java.util.Map;
import java.util.HashMap;

@SuppressWarnings("unused")
public class Parser {
	
	public static DirectoryStructure parse(String input) throws ParseException {
		InputStream inputStream = new ByteArrayInputStream(input.getBytes(Charset.forName("UTF-8")));
		return new Parser(inputStream).input();
	}
	
}

PARSER_END(Parser)

//Symbols
TOKEN : { < PLUS : "+" > }
TOKEN : { < MINUS : "-" > }
TOKEN : { < OPENCBRACKET : "{" > }
TOKEN : { < CLOSECBRACKET : "}" > }

//Keywords
TOKEN : { < USING : "using" > }
TOKEN : { < IN : "in" > }
TOKEN : { < AS : "as" > }

//Other
TOKEN : { < NAME : (["A"-"Z", "a"-"z", "0"-"9", "_", "-", "."])+ > }

TOKEN : { < QUOTE : "\"" > : STRING_STATE }

<STRING_STATE> TOKEN: {
  < ENDQUOTE : <QUOTE> > : DEFAULT | < CHAR : ~["\""] >
}

//Whitespace & Comments
SKIP : { "\n" | "\r" | " " | "\t" }
SKIP : { < "#" (~["\r", "\n"])* > }

/** Main endpoint */
DirectoryStructure input() : { Map<String, String> domains; List<Repository> repos; Subdirectory d; List<Subdirectory> subDirs = new ArrayList<Subdirectory>(); } {
  	domains = domains()
  	repos = repos()
  	(
  	  d = inside()
  	  { subDirs.add(d); }
  	)*
	<EOF>
	{ return new DirectoryStructure(domains, repos, subDirs); }
}

Repository repo() : { boolean hidden; Token name; Token domain; } {
	( < PLUS >  { hidden = false; }
	| < MINUS > { hidden = true; }
	)
	domain = < NAME >
	name = < NAME >
	{ return new Repository(name.image, domain.image, hidden); }
}

Subdirectory inside() : { Token t; List<Repository> repos; Subdirectory sd; List<Subdirectory> nested = new ArrayList<Subdirectory>(); } {
	< IN >
	t = < NAME >
	< OPENCBRACKET >
	repos = repos()
	(sd = inside()
{ nested.add(sd);
})*
	< CLOSECBRACKET >
	{ return new Subdirectory(t.image, repos, nested); }
}

List<Repository> repos() : { Repository repo; List<Repository> repos = new ArrayList<Repository>(); } {
	(
	  repo = repo()
	  { repos.add(repo); }
	)*
	{ return repos; }
}

Map<String, String> domains() : { StringBuilder builder; Token as; Token t; Map<String, String> domains = new HashMap<String, String>(); } {
	(
	  	{ builder = new StringBuilder(); }	  	< USING >
		< QUOTE >
		(
		  t = < CHAR >
		  { builder.append(t.image); }
		)*
		< ENDQUOTE >
		< AS >
		as = < NAME >
		{
		  	String result = builder.toString();
		  	if(!result.endsWith("/")) {
				result = result + "/";
		  	}
		  	domains.put(as.image, result);
		}
	)+
	{ return domains; }
}